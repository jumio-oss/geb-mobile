buildscript {
    repositories {
        mavenLocal()
        maven { url 'https://artifactory.int.jumio.com/artifactory/jumio' }
    }
    dependencies {
        classpath group: 'com.jumio.gradle', name: 'jumio-gradle-plugin', version: '1.3.2'
    }
}

plugins {
    id "com.github.samueltbrown.cucumber" version "0.8"
}


apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'

group = 'jumio.oss'
version = '1.0-SNAPSHOT'

apply plugin: 'jumio'

ext.gebVersion = '0.9.3'
ext.seleniumVersion = '2.42.2'
ext.spockVersion = '0.7-groovy-2.0'
ext.groovyVersion = '2.3.4'
ext.selendroidVersion = '0.11.0'
ext.appiumClientVersion = '2.1.0'
ext.iosClientVersion =  '0.6+'


repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    selendroid
    iosdriver
    cucumberCompile
}

cucumber {
    formats = ['pretty','json:build/cucumber.json','junit:build/cucumber.xml']
    glueDirs = ['src/test/resources/env',
                'src/test/resources/support',
                'src/test/resources/step_definitions']
    featureDirs = ['src/test/resources/features']
    //tags = ['@billing', '@important']
    monochrome = false
    strict = false
    dryRun = false

    jvmOptions {
        maxHeapSize = '512m'
        environment 'ENV', 'staging'
    }
}

dependencies {

    compile "org.codehaus.groovy:groovy-all:$groovyVersion"
    compile "org.slf4j:slf4j-api:1.7.6"
    compile 'ch.qos.logback:logback-classic:1.1.2'
    compile "org.seleniumhq.selenium:selenium-java:$seleniumVersion"

    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile("org.gebish:geb-spock:$gebVersion") {
        exclude module: "groovy"
        exclude module: "groovy-all"
    }
    compile("org.spockframework:spock-core:$spockVersion") {
        exclude module: "junit"
        exclude module: "groovy"
        exclude module: "groovy-all"
    }

    compile "org.codehaus.groovy:groovy-all:$groovyVersion"
    compile("org.gebish:geb-spock:$gebVersion") {
        exclude module: "groovy"
        exclude module: "groovy-all"
    }
    compile("org.spockframework:spock-core:$spockVersion") {
        exclude module: "junit"
        exclude module: "groovy"
        exclude module: "groovy-all"
    }

    cucumberCompile 'info.cukes:cucumber-groovy:1.1.6'
    compile 'info.cukes:cucumber-groovy:1.1.6'

    compile "io.selendroid:selendroid-standalone:$selendroidVersion"
    compile "io.selendroid:selendroid-client:$selendroidVersion"
    compile "org.uiautomation:ios-client:$iosClientVersion"
    compile ("io.appium:java-client:$appiumClientVersion")

    selendroid "io.selendroid:selendroid-standalone:${selendroidVersion}"

    iosdriver "org.uiautomation:ios-server:0.6+"
    //compile "org.uiautomation:ios-server:0.6+"

//    appium ("io.appium:java-client:$appiumClientVersion") {
//        exclude module: "selenium-java"
//    }
}



def startServer = { server, command ->
    if( System.properties.'skipServer')  { println "Skipping Server $server "; return }
    println "Starting $server with $command"
    ProcessBuilder builder = new ProcessBuilder(command.split(' '))
    builder.redirectErrorStream(true)
    builder.directory(buildDir)
    Process process = builder.start()
    project.ext[server] = process
    println "$server started : $process"
    def loggingThread = Thread.start("logging-$server") {
        FileOutputStream fout = new FileOutputStream(new File(buildDir, "${server}.log"))
        int bb
        while ((bb = process.getInputStream().read()) > 0) {
            fout.write(bb)
        }
    }
    if(System.getProperty("waitForServer")){
        println "Joining Server Logging Thread"
        loggingThread.join()
    }
}

def stopServer = { server ->
    println "Stopping $server"
    if (project.ext.has(server)) {
        Process process = ext.get(server)
        println "send CTRL-C to server"
        try {
            process.getOutputStream().write(3)
            process.getOutputStream().flush()
        }catch(e){ }
        process.destroy()
    } else {
        println "No Server $server found in process list"
    }
}

task startSelendroid(group:'Server', type: Task) << {
    description 'Start the selendroid server on port 4444 with the testapp, use -DwaitForServer=true, when you run only the server'
    startServer( "SERVER_SELENDROID" , "java -cp ${configurations.selendroid.files.toList().join(":")} io.selendroid.SelendroidLauncher -port 4444 -aut ${projectDir}/src/test/resources/testapk/selendroid-test-app-0.9.0.apk")
}

task startSelendroidForWeb(group:'Server') << {
    startServer( "SERVER_SELENDROID_WEB" , "java -cp ${configurations.selendroid.files.toList().join(":")} io.selendroid.SelendroidLauncher -port 4444")
}

task startAppium(group:'Server') << {
    startServer("SERVER_APPIUM", "appium --command-timeout 90000")
}

task startIosdriver(group:'Server') << {
    startServer( "SERVER_IOSDRIVER", "java -Djava.util.logging.config.file=${projectDir}/config/iosdriver_logging.properties -cp ${configurations.iosdriver.files.toList().join(":")} org.uiautomation.ios.server.IOSServer -beta -simulators" )
}

task stopServers(group:'Server') << {
    project.ext.properties.each{k,v ->
        if( k=~/^SERVER_/ && project.ext.has(k) ) stopServer(k)
    }
}

task runSelendroidTests(group:'Android',type: Test, dependsOn: [startSelendroid]) {

    systemProperties.put "framework", "selendroid"
    systemProperties.put "appUT_absolutePath", "${projectDir}/src/test/resources/testapk/selendroid-test-app-0.9.0.apk"
    systemProperties.put "appUT.package", "io.selendroid.testapp"
    systemProperties.put "appUT.version", "0.10.0"
    systemProperties.put "appUT_cap_Emulator", "false"
    include '**/GebMobileAutomationTestWithPagesSpec.*'
}

task runAppiumTests(group:'Android',type: Test, dependsOn: [startAppium]) {
    systemProperties.put "framework", "appium"

    systemProperties.put "appUT.package", "io.selendroid.testapp"
    systemProperties.put "appium_app", "${projectDir}/src/test/resources/testapk/selendroid-test-app-0.9.0.apk"
    systemProperties.put "appium_deviceName" , "Android"
    //To run the test on a specific device
    //System.setProperty("appium_udid","192.168.56.10:5555")
    //System.setProperty("appium_automationName","selendroid")

    //Use Appium
    include '**/UIAutomatorNavigatorTestWithPagesSpec.*'
}

task runAppiumTestsWithSelendroidDriver(group:'Android', type: Test, dependsOn: [startAppium]) {
    systemProperties.put "framework", "appium"
    systemProperties.put "appUT.package", "io.selendroid.testapp"
    systemProperties.put "appium_app", "${projectDir}/src/test/resources/testapk/selendroid-test-app-0.9.0.apk"
    systemProperties.put "appium_automationName", "selendroid"

    include '**/GebMobileAutomationTestWithPagesSpec.*'
}

task runAppiumTestsWithOldDevice(group:'Android', type: Test, dependsOn: [startAppium]) {
    systemProperties.put "framework", "appium"
    systemProperties.put "appUT.package", "io.selendroid.testapp"
    systemProperties.put "appium_app", "${projectDir}/src/test/resources/testapk/selendroid-test-app-0.9.0.apk"

    include '**/GebMobileAutomationTestWithPagesSpec.*'
}

task runIosdriverTests(group: 'Ios', type: Test, dependsOn: [startIosdriver] ) {
    systemProperties.put "appUT.package", "UICatalog"
    systemProperties.put "iosdriver_app" , ""
    systemProperties.put "framework" , "iosdriver"
    include '**/UICatalogAppSpec.*'
}

task runIosdriverInitTests(group:'Ios', type:Test, dependsOn: [startIosdriver]){
    include '**/SimpleIosMobileTest.*'
}

task runIosdriverInitTestsReal(group:'Ios', type:Test, dependsOn: [startIosdriver]){
    include '**/SimpleIosMobileTest.*'
}


tasks.withType(Test){ task->
    onlyIf { !Boolean.getBoolean('skip.tests') }
    ignoreFailures = true
    systemProperties.put = System.properties
    outputs.upToDateWhen{ false }
    doLast{
        stopServers.execute()
    }
}

task checkenv << {
    if( System.properties.'os.name'.toLowerCase().startsWith("mac") ){
        println "MAC ... "
        println "check appium installation: "+ 'appium -v'.execute().text

    }
}



ext.saucelabsUser = System.getProperty("saucelabsUser", System.getenv("SAUCE_USERNAME"))
ext.saucelabsKey  = System.getProperty("saucelabsKey", System.getenv("SAUCE_ACCESS_KEY"))

ext.saucelabsUrl  = "http://${saucelabsUser}:${saucelabsKey}@ondemand.saucelabs.com:80/wd/hub"


task appiumOnSauceLabs(type: Test) {
    systemProperties.put "framework","appium"
    systemProperties.put "appium_app","sauce-storage:selendroid-test-app-0.9.0.apk"
    systemProperties.put "selenium.url", saucelabsUrl
    systemProperties.put "appium_platformName","Android"
    systemProperties.put "appium_deviceName","LG Nexus 4 Emulator"
    systemProperties.put "appium_platformVersion","4.4"
    systemProperties.put "appium_appium-version","1.0"
    include '**/GebMobileAutomationTestWithPagesSpec.*'
}

task uploadApk2Saucelabs(type:Exec){
    commandLine = ['curl', '-u', "$saucelabsUser:$saucelabsKey", '-X', 'POST', "http://saucelabs.com/rest/v1/storage/$saucelabsUser/selendroid-test-app-0.9.0.apk?overwrite=true", '-H', 'Content-Type: application/octet-stream', '--data-binary', "@${projectDir}/src/test/resources/testapk/selendroid-test-app-0.9.0.apk" ]
}

ext.testobjectApiKey = System.getProperty("testobjectApiKey", System.getenv("TESTOBJECT_APIKEY"))
ext.testobjectUrl = System.getProperty("testobjectUrl", 'https://app.testobject.com:443/api/appium/wd/hub')

task appiumOnTestObjects(type: Test) {
    systemProperties.put "framework", "appium"
    systemProperties.put "selenium.url", testobjectUrl
    systemProperties.put "appium_appPackage" , "io.selendroid.testapp"       //important for testobject test, cause this value is not yet set on there side
    systemProperties.put 'appium_testobject_api_key', testobjectApiKey
    systemProperties.put 'appium_testobject_project', 'selendroid-test-app'
    systemProperties.put 'appium_testobject_app_id', '1'
    systemProperties.put 'appium_testobject_device', 'LG_Nexus_4_E960_real'
    include '**/GebMobileAutomationTestWithPagesSpec.*'
}

task jumioLoginTest(type: Test){
    systemProperties.put "framework", "appium"
    systemProperties.put "selenium.url", testobjectUrl
    systemProperties.put "appium_appPackage" , "com.example.jumiologin"       //important for testobject test, cause this value is not yet set on there side
    systemProperties.put 'appium_testobject_api_key', 'EDB76E8F0C9245BBBBBD34F9C4B79EE4'
    systemProperties.put 'appium_testobject_project', 'jumiologin'
    systemProperties.put 'appium_testobject_app_id', '1'
    systemProperties.put 'appium_testobject_appium_version', '1.3.5'
    systemProperties.put 'appium_testobject_device', 'LG_Nexus_5_real'
    include '**/JumioLoginTest.*'
}

